# The Two Country Quiz
model{
  # Probability of Answering Correctly
  alpha ~ dunif(0,1)    # Match
  beta1 ~ dunif(0,alpha) # Thai answer Moldovan  
  beta2 ~ dunif(0,alpha) # Moldovan answer Thai
  # Group Membership For People and Questions
    x[1] ~ dbern(1)#0 for Thai and 1 for Moldovan
    x1[1] <- x[1]+1#1 for Thai and 2 for Moldovan
  for (i in 2:nx){
    x[i] ~ dbern(0.5)#0 for Thai and 1 for Moldovan
    x1[i] <- x[i]+1#1 for Thai and 2 for Moldovan
  }
  for (j in 1:nz){
    z[j] ~ dbern(0.5)#0 for Thai and 1 for Moldovan
    z1[j] <- z[j]+1  #1 for Thai and 2 for Moldovan
  }   
  # Probability Correct For Each Person-Question Combination By Groups
  for (i in 1:nx){
    for (j in 1:nz){
      theta[i,j,1,1] <- alpha
      theta[i,j,1,2] <- beta1
      theta[i,j,2,1] <- beta2
      theta[i,j,2,2] <- alpha
    }
  }   
  # Data Are Bernoulli By Rate
  for (i in 1:nx){
    for (j in 1:nz){
      k[i,j] ~ dbern(theta[i,j,x1[i],z1[j]])
    }
  }   
}